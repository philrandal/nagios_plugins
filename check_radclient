#!/usr/bin/perl -w

# check_radclient
# Copyright (C) 2019 Michael Newton
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

use POSIX;
use strict;
use IO::Handle;
use File::Temp qw(tempfile);
use Getopt::Long qw(:config no_ignore_case bundling);
use Time::HiRes qw(gettimeofday tv_interval);

my $script = "check_radclient";
my $version = "2.0";

# default values
my $request_type;
my $host = "localhost";
my $port = 1812;
my $ipmode = 4;
my $timeout = 10;
my $secret = "";
my %avpairs = ("Message-Authenticator" => "0x00");
my $radclient_binary = "/usr/bin/radclient";
my $retries = 1;
my $send_count = 1;
my $warn_thresh = 3;
my $crit_thresh = 7;
my $perfdata = 0;
our $debug = 0;
our $verbose = 0;

# misc variables
my $command = "";
my $cmd_output = "";
my $t0;
my $elapsed;
my @avstrings;
my $avhand;
my $avfile;
my $errindex;
my %ERRORS = ('OK'=>0,'WARNING'=>1,'CRITICAL'=>2,'UNKNOWN'=>3,'DEPENDENT'=>4);

GetOptions (
    "f|function=s" => \$request_type,
    "H|host:s" => \$host,
    "p|port:1812" => \$port,
    "6|ipv6" => sub{ $ipmode = 6 },
    "t|timeout:8" => \$timeout,
    "s|secret:s" => \$secret,
    "a|avpair:s" => \%avpairs,
    "b|binary:s" => \$radclient_binary,
    "r|retries:1" => \$retries,
    "u|count:1" => \$send_count,
    "F|perf" => sub{ $perfdata = 1 },
    "w|warning:3" => \$warn_thresh,
    "c|critical:7" => \$crit_thresh,
    "d|debug" => \$debug,
    "v|verbose" => sub{ $verbose++ },
    "V|version" => sub{ printf("%s v%s\n", $script, $version); exit 1; },
    "help" => sub{ show_usage(); },
);

if (!$request_type) {
    show_usage(2);
}

($avhand, $avfile) = tempfile(UNLINK => $debug ? 0 : 1);
print_debug("File %s will not be removed at program exit", $avfile);
while (my($key, $val) = each %avpairs) {
    $val =~ s/'/\\'/g;
    print $avhand sprintf("%s='%s'\n", $key, $val);
}
close($avhand);

print_debug(
    "Parsed config: request_type = %s, host = %s:%d (IPv%d), timeout = %d, secret = %s, avfile = %s, radclient_binary = %s, warn_thresh = %d, crit_thresh = %d, debug = %d",
    $request_type, $host, $port, $ipmode, $timeout, $secret, $avfile, $radclient_binary, $warn_thresh, $crit_thresh, $debug
);

if ( $request_type eq "auth" || $request_type eq "acct" || $request_type eq "status") {
    $command = "$radclient_binary -$ipmode -q -f $avfile -c $send_count -r $retries -t $timeout $host:$port $request_type $secret";
    print_debug("radclient command to send: %s", $command);
} else {
    print_err("Unknown function %s passed, must be one of auth, acct, or status.", $request_type);
    show_usage(2);
}

$t0 = [gettimeofday()];
$cmd_output = `$command`;
$elapsed = tv_interval($t0);

if ($elapsed >= $crit_thresh && $? == 0) {
    $errindex = "CRITICAL";
} elsif ($elapsed < $warn_thresh && $? == 0) {
    $errindex = "OK";
} elsif ($elapsed >= $warn_thresh && $? == 0) {
    $errindex = "WARNING";
} else {
    $errindex = "UNKNOWN";
}

printf(
    "%s: Radius response time: %d secs, radclient exit code: %d",
    $errindex, $elapsed, $? >> 8
);

if ($perfdata) {
    printf("'radinterval'=%0.4fs;%d;%d", $elapsed, $warn_thresh, $crit_thresh);
}

print "\n";

if ($verbose > 1) {
    printf("radclient output: %s", $cmd_output);
}
exit $ERRORS{$errindex};

sub print_debug
{
    if ($main::debug || $main::verbose > 2) {
        my $fmt = shift;
        my $blue = "\033[1;34m";
        my $reset = "\033[0m";
        print STDERR sprintf($blue . "DEBUG: " . $reset . $fmt . "\n", @_);
    }
}

sub print_err
{
    my $fmt = shift;
    my $red = "\033[1;31m";
    my $reset = "\033[0m";
    print STDERR sprintf($red . "ERROR: " . $reset . $fmt . "\n", @_);
}

sub show_usage
{
    print << "USAGE"; 
\n$script $version
Usage: $script [OPTIONS]

 Operational mode:
    -f, --function=request_type

 request_type is mandatory and must be one of the following:
    auth                    test authentication (Access-Request)
    acct                    test accounting (Accounting-Request)
    stat                    test server status (Server-Status)

 Server options:
    -H, --host=SERVER       connect to SERVER (default localhost)
    -p, --port=port         connect to UDP port port (default 1812)
    -6, --ipv6              connect using IPv6
    -s, --secret=SECRET     use shared secret SECRET (default empty string)
    -t, --timeout=SECS      timeout after SECS seconds (default 10)
    -r, --retries=NUM       retry NUM times on failure or timeout (default 1)
    -C, --count=NUM         send each packet NUM times (default 1)

 Request options:
    -a, --avpair=ATTR=VAL   define an attribute/value pair for the request
                             (default Message-Authenticator=0x00)

 Nagios options:
    -w, --warning=SECS      return warning if request takes more than SECS s
                             (default 3)
    -c, --critical=SECS     return critical if request takes more than SECS s
                             (default 7)
    -F, --perfdata          include performance data in output

 Other options:
    -b, --binary=CLIENT     path to radclient if needed
                             (default /usr/bin/radclient)
    -v, --verbose           increase verbosity
    -d, --debug             enable debugging output (equivalent to -vvv)
    -h, --help              show usage information
    -V, --version           show version information

Examples:

    ./$script -f auth --host 10.20.30.40 --port 1812 -t 8 -s testing123 \
        -a User-Name=joe -a User-Password=pass -a NAS-IP-Address=192.168.0.9 \
        -b /usr/local/freeradius/bin/radclient -w 5 -c 10 -d

    ./$script --function status --host 172.16.12.6 --port 1812 --timeout 8 \
        --secret 'secret!' --binary /usr/local/bin/radclient --critical 8

    ./$script --help

USAGE
    my ($err) = @_;
    $err ||= 1;
    exit $err;
}

